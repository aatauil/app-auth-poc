# Note: no ?s are deleted with these queries, which should be OK because published data
# Updates the existing data
DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?old .
    }
} INSERT{
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?new .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/temp-ingest-graph-leidinggevenden> {
        ?s a ?type ;
           ?p ?new .
        FILTER (?type IN (
            <http://mu.semte.ch/vocabularies/ext/BestuursfunctieCode>,
            <http://www.w3.org/ns/person#Person>,
            <http://data.lblod.info/vocabularies/leidinggevenden/FunctionarisStatusCode>,
            <http://data.lblod.info/vocabularies/leidinggevenden/Functionaris>,
            <http://data.lblod.info/vocabularies/leidinggevenden/Bestuursfunctie>,
            <http://www.w3.org/ns/locn#Address>,
            <http://schema.org/ContactPoint>,
            <http://www.w3.org/ns/prov#Location>
            ))
    }
    GRAPH <http://mu.semte.ch/graphs/public> {
      ?s ?p ?old .
    }
}
;

# pushes the new data
INSERT{
  GRAPH <http://mu.semte.ch/graphs/public> {
        ?s ?p ?o .
  }
}
WHERE {
    GRAPH <http://mu.semte.ch/graphs/temp-ingest-graph-leidinggevenden> {
        ?s a ?type ;
          ?p ?o .
        FILTER (?type IN (
            <http://mu.semte.ch/vocabularies/ext/BestuursfunctieCode>,
            <http://www.w3.org/ns/person#Person>,
            <http://data.lblod.info/vocabularies/leidinggevenden/FunctionarisStatusCode>,
            <http://data.lblod.info/vocabularies/leidinggevenden/Functionaris>,
            <http://data.lblod.info/vocabularies/leidinggevenden/Bestuursfunctie>,
            <http://www.w3.org/ns/locn#Address>,
            <http://schema.org/ContactPoint>,
            <http://www.w3.org/ns/prov#Location>
            ))
    }
}
;

CLEAR GRAPH <http://mu.semte.ch/graphs/temp-ingest-graph-leidinggevenden>
